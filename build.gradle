buildscript {
    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.1"
    }
}

plugins {
    id 'java'
}

subprojects { p ->
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group 'fr.pearl.core'
    version '1.0-SNAPSHOT'

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

        flatDir {
            dir "lib"
        }
    }

    dependencies {
        if (p.name != 'api') {
            implementation project(':api')
            if (p.name != 'all' && p.name != 'core') {
                implementation project(':core')
                compileOnly name: 'spigot'
            } else {
                compileOnly files('../lib/bungee.jar')
                compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
            }
        } else {
            compileOnly files('../lib/bungee.jar')
            compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
        }
    }

    shadowJar {
        exclude 'com/cryptomorin/xseries/unused/**'
        exclude 'com/cryptomorin/xseries/messages/*'
        exclude 'com/cryptomorin/xseries/XBiome*'
        exclude 'com/cryptomorin/xseries/NMSExtras*'
        exclude 'com/cryptomorin/xseries/NoteBlockMusic*'
        exclude 'com/cryptomorin/xseries/SkullUtils*'
        exclude 'com/cryptomorin/xseries/XEntity*'
        exclude 'com/cryptomorin/xseries/XBlock*'

        relocate 'com.cryptomorin.xseries', 'fr.pearl.api.libs.xseries'
    }

    artifacts {
        archives shadowJar
    }
}